buildscript {
  repositories {
    maven { url 'https://maven.fabric.io/repo' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.20.1'
  }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
  jcenter()
  maven { url 'https://maven.fabric.io/public' }
}

def versionMajor = 2
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 23
  buildToolsVersion '21.1.2'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 23

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable 'InvalidPackage'
  }
  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

dependencies {
  compile 'se.emilsjolander:stickylistheaders:2.5.2'
  compile 'com.j256.ormlite:ormlite-android:4.47'
  compile 'com.github.bumptech.glide:glide:3.6.1'
  compile 'com.github.bumptech.glide:okhttp-integration:1.3.1'
  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
  compile 'com.google.code.gson:gson:2.3'
  compile 'com.google.android.gms:play-services-analytics:8.3.0'
  compile 'com.google.android.gms:play-services-gcm:8.3.0'
  compile 'com.android.support:cardview-v7:23.1.1'
  compile 'com.android.support:appcompat-v7:23.1.1'
  compile 'com.android.support:recyclerview-v7:23.1.1'
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
  compile 'com.squareup.okhttp:okhttp:2.4.0'
  compile 'com.squareup:otto:1.3.8'

  // Debug build
  debugCompile 'com.facebook.stetho:stetho:1.2.0'
  debugCompile 'com.facebook.stetho:stetho-okhttp:1.2.0'

  // Crashlytics Kit
  compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
    transitive = true
  }
}
