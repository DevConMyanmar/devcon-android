apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 21
  buildToolsVersion '21.1.0'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 21

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable 'InvalidPackage'
  }
  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

dependencies {
  compile project('libs:facebook')
  compile files('libs/simple.facebook-2.1.jar')
  compile 'io.realm:realm-android:0.72.0'
  compile 'com.github.bumptech.glide:glide:3.3.1'
  compile 'com.github.bumptech.glide:okhttp-integration:1.0.1'
  compile 'com.jakewharton:butterknife:5.1.1'
  compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
  compile 'com.android.support:cardview-v7:21.0.0'
  compile 'com.android.support:appcompat-v7:21.0.0'
  compile 'com.google.code.gson:gson:2.3'
  compile project(':app:libs:facebook')
}
